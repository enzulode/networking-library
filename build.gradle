plugins {
    id 'java-library'
    id 'maven-publish'
    id 'java'
}

version = '1.1.1'
group = 'com.enzulode'

repositories {
    mavenCentral()
}

dependencies {

    implementation 'org.apache.commons:commons-lang3:3.12.0'

}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/enzulode/networking-library")
            credentials {
                username = project.findProperty("gpr.user") as String ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") as String ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {

        mavenJava(MavenPublication) {
            artifactId = 'networking-library'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'Simple networking library'
                description = 'A basic java library for UDP interconnections'
                developers {
                    developer {
                        id = 'enzulode'
                        name = 'Vladislav Andreev'
                    }
                }
            }
        }

    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
