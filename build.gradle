plugins {
	id 'java-library'
	id 'maven-publish'
	id 'java'
}

version = '1.6.0'
group = 'com.enzulode'

repositories {
	mavenCentral()
}

dependencies {

	implementation 'org.apache.commons:commons-lang3:3.12.0'

}

java {
	withJavadocJar()
	withSourcesJar()
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def gitUser = properties.getProperty('gpr.user')
def gitPassword = properties.getProperty('gpr.key')

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/enzulode/networking-library")
			credentials {
				username = gitUser
				password = gitPassword
			}
		}
	}

	publications {

		mavenJava(MavenPublication) {
			artifactId = 'networking-library'
			from components.java
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}

			pom {
				name = 'Simple networking library'
				description = 'A basic java library for UDP interconnections'
				developers {
					developer {
						id = 'enzulode'
						name = 'Vladislav Andreev'
					}
				}
			}
		}

	}
}

javadoc {
	if (JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}
